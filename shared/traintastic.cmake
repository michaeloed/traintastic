# Version
set(TRAINTASTIC_VERSION 0.3.0)
set(TRAINTASTIC_CODENAME "master")

# Platform
if(UNIX AND NOT APPLE AND NOT MINGW)
  set(LINUX TRUE)
endif()

# CI/CD or local
if(DEFINED ENV{GITHUB_ACTIONS})
  message(STATUS "We're on GitHub Actions")

  if($ENV{GITHUB_REF_TYPE} MATCHES "branch")
    set(TRAINTASTIC_VERSION_EXTRA "-$ENV{CI_REF_NAME_SLUG}-$ENV{GITHUB_RUN_NUMBER}-$ENV{CI_SHA_SHORT}")
  endif()
else() # Local
  set(TRAINTASTIC_VERSION_EXTRA "-dev")
endif()

# Debian package
if(LINUX)
  execute_process(COMMAND lsb_release -i -s OUTPUT_STRIP_TRAILING_WHITESPACE OUTPUT_VARIABLE LSB_RELEASE_ID)
  execute_process(COMMAND lsb_release -c -s OUTPUT_STRIP_TRAILING_WHITESPACE OUTPUT_VARIABLE LSB_RELEASE_CODENAME)

  string(TOLOWER ${LSB_RELEASE_ID} LSB_RELEASE_ID)
  string(TOLOWER ${LSB_RELEASE_CODENAME} LSB_RELEASE_CODENAME)

  message(STATUS "lsb_release id is ${LSB_RELEASE_ID}")
  message(STATUS "lsb_release codename is ${LSB_RELEASE_CODENAME}")

  string(REPLACE "-" "~" DEBIAN_PACKAGE_VERSION_EXTRA ~${LSB_RELEASE_ID}~${LSB_RELEASE_CODENAME}${TRAINTASTIC_VERSION_EXTRA})
endif()

# Debug
message(STATUS "TRAINTASTIC_VERSION_EXTRA=${TRAINTASTIC_VERSION_EXTRA}")
message(STATUS "DEBIAN_PACKAGE_VERSION_EXTRA=${DEBIAN_PACKAGE_VERSION_EXTRA}")
